buildscript {
    repositories {
        maven {
            url 'https://maven.xillio.com/artifactory/plugins-release/'
        }
    }

    dependencies {
        classpath 'nl.xillio:gradle-config:1.2.0'
    }
}

apply plugin: 'nl.xillio.gradle-config'

configurations {
    api

    compile {
        extendsFrom api
    }
}

dependencies {
    compile 'mysql:mysql-connector-java:5.1.38'
    compile('nl.xillio.xill:plugin-jdbc-core:1.0.0-2-gc2d474c') {
        exclude(group: 'nl.xillio.xill', module: 'api')
    }
    api 'nl.xillio.xill:api:3.3.3'

    testCompile 'org.testng:testng:6.9.10'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'nl.xillio.xill:testutils:3.3.3'
}

task distZip(dependsOn: jar, type: Zip) {
    from configurations.compile.filter {
        file -> !configurations.api.contains(file)
    }
    from jar
}

buildDists.dependsOn distZip

tasks.withType(Jar) {
    doFirst {
        manifest {
            attributes "Class-Path": project.configurations.runtime.files.collect { File file -> file.name }.join(" ")
        }
    }
}
