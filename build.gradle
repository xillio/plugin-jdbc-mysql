import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        maven {
            url 'https://maven.xillio.com/artifactory/plugins-release/'
        }
    }

    dependencies {
        classpath 'nl.xillio:gradle-config:1.4.0'
    }
}

apply plugin: 'nl.xillio.gradle-config'

configurations {
    api

    compile {
        extendsFrom api
    }
}

dependencies {
    compile 'mysql:mysql-connector-java:5.1.38'
    compile('nl.xillio.xill:plugin-jdbc-core:1.0.1') {
        exclude(group: 'nl.xillio.xill', module: 'api')
    }
    api 'nl.xillio.xill:api:3.3.3'

    testCompile 'org.testng:testng:6.9.10'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'nl.xillio.xill:testutils:3.3.3'
}

task distZip(dependsOn: jar, type: Zip) {
    from configurations.compile.filter {
        file -> !configurations.api.contains(file)
    }
    from jar
}

buildDists.dependsOn distZip

tasks.withType(Jar) {
    doFirst {
        manifest {
            attributes "Class-Path": project.configurations.runtime.files.collect { File file -> file.name }.join(" ")
        }
    }
}

def bintrayInfoFile = file("$projectDir/bintray-info.json")

//noinspection GroovyAssignabilityCheck
task createBintrayInfoFile << {
    bintrayInfoFile.write(
            new File("$rootDir/gradle/bintray-info-template.json")
                    .getText('UTF-8')
                    .replace('$VERSION$', project.version.toString())
                    .replace('$BUILDDATE$', ZonedDateTime.now().format(DateTimeFormatter.ofPattern("YYYY-MM-dd'T'HH:mm:ssZ")))
    )
}

artifacts {
    archives file: bintrayInfoFile, name: 'bintray-info', type: 'json', builtBy: createBintrayInfoFile
}

clean.delete bintrayInfoFile
